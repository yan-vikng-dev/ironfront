class_name ServerApp
extends Node

@export_range(1, 65535, 1) var listen_port: int = 47_111
@export var max_clients: int = 32
@export var tick_rate_hz: int = 60
@export var arena_max_players: int = 10
@export var arena_bot_count: int = 2
@export var arena_bot_respawn_delay_seconds: float = 5.0
@export var arena_level_scene: PackedScene = preload("res://src/levels/arena/arena_level_mvp.tscn")

var tick_count: int = 0
var arena_session_state: ArenaSessionState
var metrics_logger: MetricsLogger

@onready var network_server: ENetServer = %Network
@onready var network_session: ServerSessionApi = %Session
@onready var network_gameplay: ServerGameplayApi = %Gameplay
@onready var server_arena_runtime: ServerArenaRuntime = %ArenaRuntime


func _ready() -> void:
	_apply_cli_args()
	server_arena_runtime.configure_bot_settings(arena_bot_count, arena_bot_respawn_delay_seconds)
	if not server_arena_runtime.initialize_runtime(arena_level_scene):
		get_tree().quit(1)
		return
	var arena_spawn_transforms_by_id: Dictionary[StringName, Transform2D] = (
		server_arena_runtime.get_spawn_transforms_by_id()
	)
	if arena_spawn_transforms_by_id.is_empty():
		push_error("[server][arena] startup aborted: spawn pool empty after runtime initialization")
		get_tree().quit(1)
		return
	arena_session_state = ArenaSessionState.new(arena_max_players)
	network_gameplay.configure_arena_session(arena_session_state)
	server_arena_runtime.configure_arena_session(arena_session_state)
	print("[server][arena] startup_spawn_pool count=%d" % arena_spawn_transforms_by_id.size())
	network_gameplay.configure_tick_rate(tick_rate_hz)
	server_arena_runtime.configure_network_gameplay(network_gameplay)
	network_session.configure_protocol(MultiplayerProtocol.PROTOCOL_VERSION)
	Utils.connect_checked(network_session.arena_join_requested, _on_arena_join_requested)
	Utils.connect_checked(network_session.arena_leave_requested, _on_arena_leave_requested)
	Utils.connect_checked(network_server.arena_peer_disconnected, _on_arena_peer_disconnected)
	Utils.connect_checked(
		network_gameplay.arena_input_intent_received, _on_arena_input_intent_received
	)
	Utils.connect_checked(network_gameplay.arena_fire_requested, _on_arena_fire_requested)
	Utils.connect_checked(
		network_gameplay.arena_shell_select_requested, _on_arena_shell_select_requested
	)
	Utils.connect_checked(network_gameplay.arena_respawn_requested, _on_arena_respawn_requested)
	if not network_server.start_server(listen_port, max_clients):
		get_tree().quit(1)
		return
	print(
		(
			"[server][arena] global_session_started created_at=%d max_players=%d"
			% [arena_session_state.created_unix_time, arena_session_state.max_players]
		)
	)
	metrics_logger = MetricsLogger.new(self)
	Engine.physics_ticks_per_second = tick_rate_hz
	print("[server] physics tick loop configured at %d Hz" % Engine.physics_ticks_per_second)


func _reject_arena_join(peer_id: int, reason: String) -> void:
	print("[server][join] reject_join_arena peer=%d reason=%s" % [peer_id, reason])
	network_session.reject_arena_join(peer_id, reason)


func _apply_cli_args() -> void:
	var client_args: Dictionary = Env.get_parsed_cmdline_user_args()
	listen_port = Env.get_env("port", listen_port)
	arena_bot_count = Env.get_env("bot-count", arena_bot_count)
	arena_bot_respawn_delay_seconds = Env.get_env(
		"bot-respawn-delay", arena_bot_respawn_delay_seconds
	)


func _on_arena_join_requested(peer_id: int, ticket: String) -> void:
	if arena_session_state.has_peer(peer_id):
		_remove_arena_peer(peer_id, "REJOIN_REQUEST")
	if ticket.strip_edges().is_empty():
		_reject_arena_join(peer_id, "INVALID TICKET")
		return
	var claims: Dictionary = PlayTicketVerifier.verify_and_extract(
		ticket, AppConfig.ticket_verification_public_key
	)
	if claims.is_empty():
		_reject_arena_join(peer_id, "TICKET VERIFICATION FAILED")
		return
	var player_name: String = str(claims.get("username", ""))
	if player_name.is_empty():
		_reject_arena_join(peer_id, "INVALID PLAYER NAME")
		return
	var join_result: Dictionary = arena_session_state.try_join_peer(
		peer_id, player_name, claims.get("loadout", {})
	)
	if not join_result.get("success", false):
		_reject_arena_join(peer_id, str(join_result.get("message", "JOIN FAILED")))
		return
	var tank_spec: TankSpec = join_result.get("tank_spec", null)
	var spawn_result: Dictionary = server_arena_runtime.spawn_peer_tank_at_random(
		peer_id, player_name, tank_spec
	)
	if not spawn_result.get("success", false):
		arena_session_state.remove_peer(peer_id, "NO_SPAWN_AVAILABLE")
		_reject_arena_join(peer_id, "NO SPAWN AVAILABLE")
		return
	var spawn_transform: Transform2D = spawn_result.get("spawn_transform", Transform2D.IDENTITY)
	arena_session_state.set_peer_authoritative_state(
		peer_id, spawn_transform.origin, spawn_transform.get_rotation(), Vector2.ZERO
	)
	network_session.complete_arena_join(
		peer_id,
		str(join_result.get("message", "JOINED")),
		spawn_transform.origin,
		spawn_transform.get_rotation()
	)
	network_gameplay.broadcast_state_snapshot_now()
	print(
		(
			"[server][arena] player_joined peer=%d spawn_id=%s"
			% [peer_id, spawn_result.get("spawn_id", "")]
		)
	)


func _on_arena_leave_requested(peer_id: int) -> void:
	var removed: bool = _remove_arena_peer(peer_id, "CLIENT_REQUEST")
	var leave_message: String = "LEFT ARENA" if removed else "NOT IN ARENA"
	network_session.complete_arena_leave(peer_id, leave_message)


func _on_arena_peer_disconnected(peer_id: int) -> void:
	_remove_arena_peer(peer_id, "PEER_DISCONNECTED")


func _remove_arena_peer(peer_id: int, reason: String) -> bool:
	var remove_result: Dictionary = arena_session_state.remove_peer(peer_id, reason)
	if not remove_result.get("removed", false):
		return false
	server_arena_runtime.despawn_peer_tank(peer_id, reason)
	network_gameplay.broadcast_state_snapshot_now()
	print("[server][arena] peer_removed_cleanup peer=%d reason=%s" % [peer_id, reason])
	return true


func _on_arena_input_intent_received(
	peer_id: int,
	input_tick: int,
	left_track_input: float,
	right_track_input: float,
	turret_aim: float
) -> void:
	if not arena_session_state.has_peer(peer_id):
		return
	if input_tick <= 0:
		return
	var is_too_far_future: bool = (
		input_tick
		> (
			arena_session_state.get_peer_last_input_tick(peer_id)
			+ MultiplayerProtocol.MAX_INPUT_FUTURE_TICKS
		)
	)
	if is_too_far_future:
		print("[server][sync][input] ignored_far_future peer=%d tick=%d" % [peer_id, input_tick])
		return
	var received_msec: int = Time.get_ticks_msec()
	var accepted: bool = arena_session_state.set_peer_input_intent(
		peer_id,
		input_tick,
		clamp(left_track_input, -1.0, 1.0),
		clamp(right_track_input, -1.0, 1.0),
		clamp(turret_aim, -1.0, 1.0),
		received_msec
	)
	if not accepted:
		print("[server][sync][input] ignored_non_monotonic peer=%d tick=%d" % [peer_id, input_tick])
		return
	network_gameplay.mark_input_applied()


func _on_arena_fire_requested(peer_id: int, fire_request_seq: int) -> void:
	if not arena_session_state.has_peer(peer_id):
		return
	var received_msec: int = Time.get_ticks_msec()
	var accepted: bool = arena_session_state.queue_peer_fire_request(
		peer_id, fire_request_seq, received_msec
	)
	if not accepted:
		print(
			(
				"[server][sync][fire] ignored_non_monotonic peer=%d seq=%d"
				% [peer_id, fire_request_seq]
			)
		)
		return
	network_gameplay.mark_fire_request_applied()


func _on_arena_shell_select_requested(
	peer_id: int, shell_select_seq: int, shell_id: String
) -> void:
	if not arena_session_state.has_peer(peer_id):
		return
	var received_msec: int = Time.get_ticks_msec()
	var accepted: bool = arena_session_state.queue_peer_shell_select_request(
		peer_id, shell_select_seq, shell_id, received_msec
	)
	if not accepted:
		print(
			(
				(
					"[server][sync][shell_select] ignored_non_monotonic_or_invalid "
					+ "peer=%d seq=%d path=%s"
				)
				% [peer_id, shell_select_seq, shell_id]
			)
		)


func _on_arena_respawn_requested(peer_id: int) -> void:
	if not arena_session_state.has_peer(peer_id):
		return
	var peer_state: Dictionary = arena_session_state.get_peer_state(peer_id)
	var player_name: String = str(peer_state.get("player_name", ""))
	var tank_spec: TankSpec = arena_session_state.get_peer_tank_spec(peer_id)
	if tank_spec == null:
		return
	var respawn_result: Dictionary = server_arena_runtime.respawn_peer_tank_at_random(
		peer_id, player_name, tank_spec
	)
	if not respawn_result.get("success", false):
		var reason: String = str(respawn_result.get("reason", "RESPAWN_FAILED"))
		if reason == "NO_SPAWN_AVAILABLE":
			push_warning("[server][arena] respawn rejected peer=%d reason=%s" % [peer_id, reason])
		return
	var spawn_id: StringName = respawn_result.get("spawn_id", StringName())
	var spawn_transform: Transform2D = respawn_result.get("spawn_transform", Transform2D.IDENTITY)
	var reset_loadout: bool = arena_session_state._reset_peer_loadout_to_entry_state(peer_id)
	if not reset_loadout:
		push_warning("[server][arena] respawn_loadout_reset_failed peer=%d" % peer_id)
	arena_session_state.clear_peer_control_intent(peer_id)
	arena_session_state.set_peer_authoritative_state(
		peer_id, spawn_transform.origin, spawn_transform.get_rotation(), Vector2.ZERO, 0.0
	)
	network_gameplay.broadcast_arena_respawn(
		peer_id,
		player_name,
		tank_spec.tank_id,
		spawn_transform.origin,
		spawn_transform.get_rotation()
	)
	print("[server][arena] player_respawned peer=%d spawn_id=%s" % [peer_id, spawn_id])


func _physics_process(delta: float) -> void:
	tick_count += 1
	var authoritative_player_states: Array[Dictionary] = (
		server_arena_runtime.step_authoritative_runtime(arena_session_state, delta)
	)
	network_gameplay.set_authoritative_player_states(authoritative_player_states)
	network_gameplay.on_server_tick(tick_count, delta)
	metrics_logger.log_periodic()
